/*
 * Copyright 2015-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task generateMinPom {
    doLast {
        String pomHeader = """|<?xml version="1.0" encoding="UTF-8"?>
            |<project xmlns="http://maven.apache.org/POM/4.0.0"
            |         xsi:schemaLocation="http://maven.apache.org/POM/4.0.0 http://maven.apache.org/xsd/maven-4.0.0.xsd"
            |         xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">
            |    <modelVersion>4.0.0</modelVersion>
            |    <groupId>${project.group}</groupId>
            |    <artifactId>${project.name}</artifactId>
            |    <version>${project.version}</version>
            |""".stripMargin('|')

        def dependencyTemplate = { dep -> """|        <dependency>
                                             |            <groupId>$dep.group</groupId>
                                             |            <artifactId>$dep.name</artifactId>
                                             |            <version>$dep.version</version>
                                             |        </dependency>""".stripMargin('|')
        }

        def optionalDependencyTemplate = { dep -> """|        <dependency>
                                                     |            <groupId>$dep.group</groupId>
                                                     |            <artifactId>$dep.name</artifactId>
                                                     |            <version>$dep.version</version>
                                                     |            <optional>true</optional>
                                                     |        </dependency>""".stripMargin('|')
        }

        def dependencyEntry = { dep ->
            project.findProperty('optionalDeps') && project.optionalDeps.contains(dep) ? optionalDependencyTemplate(dep) : dependencyTemplate(dep)
        }

        String deps = configurations.runtime.allDependencies.findAll({it.name!= 'unspecified'})
            .collect({ dep -> dependencyEntry(dep)}).join('\n')

        String pom = pomHeader
        if (deps) {
            pom += "    <dependencies>\n$deps\n    </dependencies>\n"
        }
        pom += "</project>"

        project.file("$buildDir/tmp/maven").mkdirs()
        project.file("$buildDir/tmp/maven/pom.xml").text = pom
        project.file("$buildDir/tmp/maven/pom.properties").text = """|# Generated by Gradle ${gradle.gradleVersion}
            |version=${project.version}
            |groupId=${project.group}
            |artifactId=${project.name}
            |""".stripMargin('|')
    }
}

if (Boolean.valueOf((rootProject.findProperty('release') ?: false))) {
    jar {
        dependsOn 'generateMinPom'
        manifest {
            attributes(
                'Created-By': buildCreatedBy,
                'x-Built-By': buildBy,
                'x-Build-Jdk': buildJdk,
                'x-Build-Date': buildDate,
                'x-Build-Time': buildTime,
                'x-Build-Revision': buildRevision,
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Specification-Vendor': project.project_vendor,
                'Automatic-Module-Name': 'javax.application'
            )
        }

        metaInf {
            from(rootProject.file('.')) {
                include 'LICENSE'
            }
            from(file("$buildDir/tmp/maven")) {
                into "maven/${project.group}/${project.name}"
            }
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.project_description
                url = project.project_url
                inceptionYear = '2015'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = project.project_scm
                }
                developers {
                    [
                        aalmiray : 'Andres Almiray'
                    ].each { devId, devName ->
                        developer {
                            id = devId
                            name = devName
                            roles = ['JSR-377 Spec lead']
                        }
                    }
                }
            }
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''
if (!project.hasProperty('mavenUsername')) ext.mavenUsername = ''
if (!project.hasProperty('mavenPassword')) ext.mavenPassword = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = project.project_bintray_repo
        userOrg = project.project_bintray_org
        name = project.name
        desc = project.project_description
        licenses = ['Apache-2.0']
        labels = ['desktop', 'java', 'jsr377']
        websiteUrl = project.project_url
        issueTrackerUrl = project.project_issues
        vcsUrl = project.project_scm
        publicDownloadNumbers = true
        githubRepo = 'jsr377/jsr377-api'
        version {
            name = project.version
            vcsTag = "${project.name}-${project.version}"
            mavenCentralSync {
                sync = true
                user = project.mavenUsername
                password = project.mavenPassword
            }
        }
    }
}
