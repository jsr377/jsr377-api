/*
 * Copyright 2015-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'org.kordamp.gradle:project-gradle-plugin:0.11.0'
        classpath 'org.ajoberstar:gradle-git:1.7.2'
        classpath 'gradle.plugin.gradle-java9-collision:plugin:0.2.3'
    }
}

apply plugin: 'org.kordamp.gradle.project'

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''
if (!project.hasProperty('sonatypeUsername')) ext.sonatypeUsername = ''
if (!project.hasProperty('sonatypePassword')) ext.sonatypePassword = ''

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name          = 'jsr377-api'
        vendor        = 'JSR-377'
        description   = 'JSR 377 - Desktop|Embedded Application API'
        inceptionYear = '2015'
        tags          = ['desktop', 'java', 'jsr377']

        links {
            website      = 'https://github.com/jsr377/jsr377-api'
            issueTracker = 'https://github.com/jsr377/jsr377-api/issues'
            scm          = 'https://github.com/jsr377/jsr377-api.git'
        }

        people {
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                roles = ['Spec Lead', 'Developer']
            }
        }

        specification {
            title = 'JSR-377 API'
        }

        implementation { enabled = false }

        credentials {
            sonatype {
                username = project.sonatypeUsername
                password = project.sonatypePassword
            }
        }
    }

    license {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
        userOrg = 'jsr377'
        name    = 'jsr377-api'
    }

    stats {
        formats = ['xml', 'txt']
    }

    javadoc {

        options {
            links('http://docs.oracle.com/javase/8/docs/api')
        }
    }
}

allprojects {
    apply plugin: 'idea'

    repositories {
        jcenter()
    }
}

idea {
    project {
        jdkName rootProject.sourceCompatibility
        languageLevel rootProject.sourceCompatibility
    }
}

subprojects { subproj ->
    plugins.withType(JavaPlugin) {
        subproj.tasks.withType(JavaCompile) {
            sourceCompatibility = rootProject.sourceCompatibility
            targetCompatibility = rootProject.sourceCompatibility
            options.compilerArgs = ['-Xlint:all', '-Xlint:deprecation', '-Xlint:unchecked']
        }
    }
}
